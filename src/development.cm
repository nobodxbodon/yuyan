Library
  structure YuLangLexer
  structure RawAST
  structure PrettyPrint
  structure ReplTest
  functor YuLangParseFn

  structure UTF8Char
  structure UTF8CharOrdered
  structure UTF8String
  structure UTF8StringOrdered
  structure UID
  structure MixedStr

  structure SpecialChars

  structure ElaboratePrecedence 
  structure DeclarationParser
  signature PARSER_OPTIONS
  structure MixFixParser
  functor PrecedenceParser
  structure Operators
  structure OpAST
  structure ParseAST
  structure BracketScanner

  structure InteractiveRepl 
  structure TypeCheckAndEval


  structure BuiltinOperators 
  structure NatOps
  structure GroupOps
  structure FunctionOps
  structure DeclarationOps
  structure OperatorRegistry

  structure StatementAST 
  structure StatementDivisionPass

  structure TypeCheckingPass
  structure TypeCheckingAST
  structure PreprocessingPass
  structure PreprocessingAST
  structure SignatureParsingOrchestrator
  structure ExpressionConstructionPass 
  structure TypeCheckingASTOps 


  structure KMachine
  structure PersistentKMachine
  structure ErasurePass
  
is
  ../cmlib/cmlib.cm
  (* $/basis.cm *)
  (* $/smlnj-lib.cm *)
  $/ml-lpt-lib.cm 

  parser/rawAST.sml
  parser/elaboratePrecedence.sml
  parser/bracket-scanner.sml
  parser/declaration-parser.sml
  parser/parser-options.sig
  parser/parseAST.sml
  parser/mixfixParser.sml
  parser/precedenceParser.sml


  ui/repl.sml
  ui/type-check-and-eval.sml
  ui/prettyPrint.sml
  
  builtinOperators/builtin.sml
  builtinOperators/natOps.sml
  builtinOperators/groupOps.sml
  builtinOperators/functionOps.sml
  builtinOperators/specialChars.sml
  builtinOperators/declarationOps.sml
  builtinOperators/operator-registry.sml
  builtinOperators/operators.sml
  builtinOperators/op-ast.sml

  util/utf8char.sml
  util/utf8string.sml
  util/ordered.sml
  util/uniqueId.sml
  util/mixed-str.sml

  passes/statement/statement-ast.sml
  passes/statement/statement-division-pass.sml
  passes/preprocess/preprocessing-pass.sml
  passes/preprocess/preprocessing-ast.sml
  passes/preprocess/expression-construction-pass.sml
  passes/preprocess/signature-parsing-orchestrator.sml
  passes/typechecking/type-checking-ast.sml
  passes/typechecking/type-checking-pass.sml
  passes/typechecking/type-checking-ast-ops.sml
  passes/erase/erasure-pass.sml
  
  execution/k-machine.sml
  execution/persistent-k-machine.sml

  replTest.sml
  lexer/yulang.lex : ml-ulex
  lexer/yulang.grm : ml-antlr
